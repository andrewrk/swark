#!/usr/bin/env coffee

dcpu16 = require("dcpu16")
fs = require("fs")

filename = process.argv[2]
unless filename?
  process.stderr.write "Usage: dcpu16exe [filename.dasm]\n"
  process.exit 1
asm_code = fs.readFileSync(filename, 'utf8')

cpu = new dcpu16.CPU()

# stdin/stdout
cpu.mapDevice 0x8fff, 1, do ->
  buffer = ""
  already_listening = false
  listen = ->
    return if already_listening
    already_listening = true
    stdin = process.openStdin()
    stdin.on 'data', (buf) -> buffer += buf
  return {} =
    get: ->
      listen()
      result = buffer.charCodeAt(0) or 0x0000
      buffer = buffer.substr(1)
      result
    set: (index, value) ->
      process.stdout.write String.fromCharCode(value)

# write anything here to kill the program
cpu.mapDevice 0x8ffe, 1,
  set: -> cpu.stop()

assembler = new dcpu16.Assembler(cpu)
try
  assembler.compile asm_code
catch err
  process.stderr.write "#{err}\n"
  process.exit 1

cpu.run()
